buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

// Fetch version from source because having to write version in two separate place is dumb
version = getVersionFromSource()
String getVersionFromSource(){
    File file = file("src/main/java/tictim/ceu/CeuMod.java")
    String version = file.find { String s->
        return s.contains("String VERSION")
    }
    return version.substring(version.indexOf('"')+1, version.lastIndexOf('"'))
}

group = "tictim.ceu"
archivesBaseName = "CEU-1.12.2"

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    maven {
        name = "ic2"
        url = "https://maven.ic2.player.to/"
    }
    maven {
        name = "ChickenBones maven"
        url = "https://chickenbones.net/maven/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

    implementation fg.deobf("codechicken:CodeChickenLib:1.12.2-3.2.3.358:deobf")
    implementation "gregtechce:gregtech:1.12.2:1.14.0.689"
    implementation 'net.industrial-craft:industrialcraft-2:2.8.111-ex112:api'

    // compile against the JEI API but do not include it at runtime
    compileOnly "mezz.jei:jei_1.12.2:4.15.0.268:api"
    // at runtime, use the full JEI jar
    runtimeOnly "mezz.jei:jei_1.12.2:4.15.0.268"
}

jar {
    manifest {
        attributes([
                "Specification-Title": "CEU",
                "Specification-Vendor": "Tictim",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Tictim",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}